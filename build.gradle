allprojects {
	version = '1.4.0-SNAPSHOT'
	group = 'org.corewall'
	sourceCompatibility = '1.5'
	targetCompatibility = '1.5'
}

defaultTasks 'clean', 'package'

task clean << {
	ant.delete(dir: file('dist'), failonerror: false)
}

/**
 * Packaging tasks.	 It may be good to just extend the gradle dist task instead of having a custom task.
 */
task 'package-check'(dependsOn: [':app:build']) << {
	def required = file("scenegraph/dist/scenegraph-${version}.jar")
	if (!required.exists()) {
		logger.error("!! Missing required scenegraph library or version mismatch.  Expected: scenegraph/dist/scenegraph-${version}.jar")
		logger.error("!! The library must be generated (or re-generated for this version) via 'gradle :scenegraph:build-jni'")
		logger.error("!! See scenegraph/README for instructions on how to setup the build environment")
		throw new Error("Missing required scenegraph library or version mismatch.  Expected: scenegraph/dist/scenegraph-${version}.jar")
	}
}

/**
 * Package things up as a Mac application bundle
 */
task 'package-mac'(dependsOn: 'package-check') << {
	def app = file('dist/mac/Corelyzer.app')
	if (app.exists()) {
		// copy the jars and native libraries
		copy {
			into 'dist/mac/Corelyzer.app/Contents/Resources/Java'
			from 'app/dist'
			from 'app/deps/mac'
			from file('scenegraph/dist').listFiles().findAll { it.name.endsWith('.jar') || it.name.endsWith('.jnilib') }
		}
	} else {
		mkdir(app)

		// copy the skeleton
		copy {
			into app
			from 'packages/mac'
		}

		// copy the base resources and help files
		copy {
			into 'dist/mac/Corelyzer.app/Contents/Resources'
			from 'packages/base/resources'
		}
		copy {
			into 'dist/mac/Corelyzer.app/Contents/help'
			from 'packages/base/help'
		}

		// copy the jars and native libraries
		copy {
			into 'dist/mac/Corelyzer.app/Contents/Resources/Java'
			from 'app/dist'
			from 'app/deps/mac'
			from file('scenegraph/dist').listFiles().findAll { it.name.endsWith('.jar') || it.name.endsWith('.jnilib') }
		}

		// update our Info.plist
		def classpath = file('dist/mac/Corelyzer.app/Contents/Resources/Java').listFiles().findAll { it.name.endsWith('.jar') }.collect { file ->
			'\t\t\t<string>\\$JAVAROOT/' + file.name + '</string>'
		}
		def plist = file('dist/mac/Corelyzer.app/Contents/Info.plist')
		def text = plist.text.toString()
		text = text.replaceAll(':version', version) // add our version
		text = text.replaceAll(':classpath', classpath.join('\n'))
		plist.write(text)

		ant {
			// make our app executable
			chmod(file:"dist/mac/Corelyzer.app/Contents/MacOS/Corelyzer", perm:"ugo+rx")

			// make a tarball
			exec(executable: 'tar', dir: 'dist/mac') {
				arg(value: 'czvf')
				arg(value: "Corelyzer-${version}.tar.gz")
				arg(value: 'Corelyzer.app')
			}
		}
	}
}

task 'package-win'(dependsOn: 'package-check') << {
	def dist = file("dist/win/Corelyzer")
	def libs = file('dist/win/Corelyzer/lib')
	if (dist.exists()) {
		// copy the jars to Corelyzer/lib
		copy {
			into libs
			from 'app/dist'
			from 'app/deps/win'
			from file('scenegraph/dist').listFiles().findAll { it.name.endsWith('.jar') }
		}

		// copy dlls to Corelyzer since I couldn't find a way to make Windows find them in lib
		copy {
			into dist
			from 'app/deps/win'
			from file('scenegraph/dist').listFiles().findAll { it.name.endsWith('.dll') }
		}
	} else {
		mkdir(dist)
		mkdir(libs)

		// copy the skeleton
		copy {
			into dist
			from 'packages/win'
		}

		// copy the base resources and help files
		copy {
			into dist
			from 'packages/base'
		}

		// copy the jars to Corelyzer/lib
		copy {
			into libs
			from 'app/dist'
			from 'app/deps/win'
			from file('scenegraph/dist').listFiles().findAll { it.name.endsWith('.jar') }
		}

		// copy dlls to Corelyzer since I couldn't find a way to make Windows find them in lib
		copy {
			into dist
			from 'app/deps/win'
			from file('scenegraph/dist').listFiles().findAll { it.name.endsWith('.dll') }
		}

		// create our zip file
		ant.zip(basedir: file("dist/win"), destfile: file("dist/win/Corelyzer-${version}.zip"), includes: "Corelyzer/**")

		// TODO create installer
	}
}

task 'package'(dependsOn: ['package-mac', 'package-win']) << {}

task wrapper(type: Wrapper) {
	gradleVersion = '0.9.2'
	jarFile = 'wrapper/gradle-wrapper.jar'
}
